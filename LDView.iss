; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
AppName=LDView
AppVerName=LDView 3.1
AppVersion=3.1.0.2
OutputBaseFilename=LDView-3100
SourceDir=L:\TEMP\Releases\LDView-3100
AppPublisher=Travis Cobbs
AppPublisherURL=http://ldview.sourceforge.net/
AppSupportURL=http://ldview.sourceforge.net/
AppUpdatesURL=http://ldview.sourceforge.net/
Compression=zip/9
DefaultDirName={reg:HKCU\Software\Travis Cobbs\LDView,InstallPath|{pf}\LDView}
DefaultGroupName=LDView
OutputDir=Setup
AllowNoIcons=yes
LicenseFile=License.txt
SetupIconFile=LDViewIcon.ico
ChangesAssociations=yes

[Tasks]
Name: desktopicon; Description: "Create a &desktop icon"; GroupDescription: "Additional icons:"; Flags: unchecked
Name: quicklaunchicon; Description: "Create a &Quick Launch icon"; GroupDescription: "Additional icons:"; Flags: unchecked
Name: screensaver; Description: "Install LDView as a screen saver"; GroupDescription: "Screen Saver:"
Name: registerfiles; Description: "Use LDView to open LDraw models"; GroupDescription: "File Associations:"; Flags: unchecked

[Files]
Source: "LDView.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "license.txt"; DestDir: "{app}"; Flags: ignoreversion
Source: "Readme.txt"; DestDir: "{app}"; Flags: ignoreversion isreadme
Source: "ChangeHistory.html"; DestDir: "{app}"; Flags: ignoreversion
Source: "Help.html"; DestDir: "{app}"; Flags: ignoreversion
Source: "m6459.ldr"; DestDir: "{app}"; Flags: ignoreversion
Source: "8464.mpd"; DestDir: "{app}"; Flags: ignoreversion
Source: "LDView.hlp"; DestDir: "{app}"; Flags: ignoreversion
Source: "LDView Home Page.url"; DestDir: "{app}"; Flags: ignoreversion
Source: "LDView.exe"; DestDir: "{sys}"; DestName: "LDView.scr"; Flags: ignoreversion; Tasks: screensaver
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\LDView"; Filename: "{app}\LDView.exe"
Name: "{group}\LDView Home Page"; Filename: "{app}\LDView Home Page.url"
Name: "{group}\Uninstall LDView"; Filename: "{uninstallexe}"
Name: "{userdesktop}\LDView"; Filename: "{app}\LDView.exe"; Tasks: desktopicon
Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\LDView"; Filename: "{app}\LDView.exe"; Tasks: quicklaunchicon

[Run]
Filename: "{app}\LDView.exe"; Parameters: """{app}\m6459.ldr"""; Description: "Launch LDView"; Flags: nowait postinstall skipifsilent

[Registry]
Root: HKCR; Subkey: ".ldr"; ValueType: string; ValueName: ""; ValueData: "LDView.ldr"; Flags: uninsdeletevalue; Tasks: registerfiles
Root: HKCR; Subkey: "LDView.ldr"; ValueType: string; ValueName: ""; ValueData: "LDraw Model"; Flags: uninsdeletekey; Tasks: registerfiles
Root: HKCR; Subkey: "LDView.ldr\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\LDView.exe,3"; Tasks: registerfiles
Root: HKCR; Subkey: "LDView.ldr\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\LDView.exe"" ""%1"""; Tasks: registerfiles

Root: HKCR; Subkey: ".mpd"; ValueType: string; ValueName: ""; ValueData: "LDView.mpd"; Flags: uninsdeletevalue; Tasks: registerfiles
Root: HKCR; Subkey: "LDView.mpd"; ValueType: string; ValueName: ""; ValueData: "LDraw Multi-Part Dat"; Flags: uninsdeletekey; Tasks: registerfiles
Root: HKCR; Subkey: "LDView.mpd\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\LDView.exe,4"; Tasks: registerfiles
Root: HKCR; Subkey: "LDView.mpd\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\LDView.exe"" ""%1"""; Tasks: registerfiles

